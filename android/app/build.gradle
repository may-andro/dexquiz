plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.mayandro.dexquiz"
    compileSdkVersion 34
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.mayandro.dexquiz"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            productFlavors.all { flavor ->
                if (flavor.name == "kanto") {
                    flavor.signingConfig signingConfigs.kanto
                } else if (flavor.name == "johto") {
                    flavor.signingConfig signingConfigs.johto
                } else {
                    flavor.signingConfig signingConfigs.kanto
                }
            }
        }
    }

    signingConfigs {
        kanto {
            storeFile keystoreProperties['kantoStoreFile'] ? file(keystoreProperties['kantoStoreFile']) : null
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']

            v2SigningEnabled true
        }
        johto {
            storeFile keystoreProperties['johtoStoreFile'] ? file(keystoreProperties['johtoStoreFile']) : null
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']

            v2SigningEnabled true
        }
    }

    flavorDimensions "region"

    productFlavors {
        kanto {
            dimension "region"
            applicationIdSuffix ".kanto"
        }
        johto {
            dimension "region"
            applicationIdSuffix ".johto"
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.google.android.material:material:1.11.0'
}